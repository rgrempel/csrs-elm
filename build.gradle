buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${spring_boot_version}")
        classpath('com.moowork.gradle:gradle-node-plugin:0.8')    
        classpath('com.moowork.gradle:gradle-gulp-plugin:0.1')        
    }
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'spring-boot'
apply plugin: 'war'

apply from: 'yeoman.gradle'
apply from: 'liquibase.gradle'
apply from: 'gatling.gradle'

// apply from: 'querydsl.gradle'
apply from: 'jpa_meta_model.gradle'

apply plugin: 'eclipse'

group = 'com.fulliautomatix.csrs'
version = '1.0.1'
description = ''

war {
    baseName = 'csrs'
    version = '1.0.1'
}

if (project.hasProperty('prod')) {
    profile = 'prod'
} else if (project.hasProperty('fast')) {
    profile = 'fast'
}

if (profile == 'prod') {
    apply from: 'profile_prod.gradle'
} else if (profile == 'fast') {
    apply from: 'profile_fast.gradle'
} else {
    apply from: 'profile_dev.gradle'
}

task 'gulp' (type: Exec) {
    commandLine('node_modules/.bin/gulp')

    outputs.dir 'build/resources/main/public'

    inputs.dir 'src/main/elm'
    inputs.file 'bower.json'
    inputs.file 'gulpfile.js'
}

processResources {
    dependsOn gulp

    doFirst {
        checkTemplate('src/main/resources/config/application-dev.yml');
        checkTemplate('src/main/resources/config/application-prod.yml');
        checkTemplate('src/main/resources/config/application.yml');
    }
}

processTestResources {
    doFirst {
        checkTemplate('src/test/resources/config/application.yml');
    }
}

def checkTemplate (f) {
    if (!file(f).exists()) {
        ant.fail("You must create the file ${f} by copying the template in that directory and customizing it.");
    }
}

sourceCompatibility=1.8
targetCompatibility=1.8

bootRepackage {
   mainClass = 'com.fulliautomatix.csrs.Application'
}

springBoot {
    mainClass = 'com.fulliautomatix.csrs.Application'
}

bootRun {
    addResources = false
}

configurations {
    providedRuntime
}

apply from: 'local.gradle'

repositories {
    maven { url 'http://repo.spring.io/milestone' }
    maven { url 'http://repo.spring.io/snapshot' }
    maven { url 'https://repository.jboss.org/nexus/content/repositories/releases' }
    maven { url 'https://oss.sonatype.org/content/repositories/releases' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url 'http://repo.maven.apache.org/maven2' }
}

dependencies {
    providedCompile group: 'org.projectlombok', name: 'lombok', version: '1.16.4'

    compile group: 'com.voodoodyne.jackson.jsog', name: 'jackson-jsog', version: '1.1'
    
    compile(group: 'org.apache.xmlgraphics', name: 'fop', version: '1.1') {
        exclude(module: 'avalon-framework-api')
        exclude(module: 'avalon-framework-impl')
    }
    compile group: 'org.apache.avalon.framework', name: 'avalon-framework-api', version: '4.3.1'
    compile group: 'org.apache.avalon.framework', name: 'avalon-framework-impl', version: '4.3.1'

    // Standard for JHipster

    compile group: 'io.dropwizard.metrics', name: 'metrics-core'
    compile group: 'io.dropwizard.metrics', name: 'metrics-annotation', version: dropwizard_metrics_version
    compile group: 'io.dropwizard.metrics', name: 'metrics-ehcache', version: dropwizard_metrics_version
    compile group: 'io.dropwizard.metrics', name: 'metrics-graphite'
    compile group: 'io.dropwizard.metrics', name: 'metrics-healthchecks'
    compile group: 'io.dropwizard.metrics', name: 'metrics-jvm', version: dropwizard_metrics_version
    compile group: 'io.dropwizard.metrics', name: 'metrics-servlet', version: dropwizard_metrics_version
    compile group: 'io.dropwizard.metrics', name: 'metrics-json', version: dropwizard_metrics_version
    compile group: 'io.dropwizard.metrics', name: 'metrics-servlets'
    
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-json-org', version: jackson_version
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-hppc', version: jackson_version
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-joda', version: jackson_version
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-hibernate4', version: jackson_version
    
    compile (group: 'com.ryantenney.metrics', name: 'metrics-spring', version: metrics_spring_version) {
        exclude(module: 'metrics-core')
        exclude(module: 'metrics-healthchecks')
    } 
    
    compile(group: 'com.zaxxer', name: 'HikariCP') {
        exclude(module: 'tools')
    }
    
    compile group: 'commons-lang', name: 'commons-lang', version: commons_lang_version
    compile group: 'commons-io', name: 'commons-io', version: commons_io_version

    compile group: 'javax.inject', name: 'javax.inject', version: javax_inject_version
    compile group: 'javax.transaction', name: 'javax.transaction-api'
    
    compile group: 'joda-time', name: 'joda-time'
    compile group: 'joda-time', name: 'joda-time-hibernate', version: joda_time_hibernate_version
    
    compile group: 'org.apache.geronimo.javamail', name: 'geronimo-javamail_1.4_mail', version: geronimo_javamail_1_4_mail_version
    
    compile group: 'org.hibernate', name: 'hibernate-core', version: hibernate_entitymanager_version
    compile group: 'org.hibernate', name: 'hibernate-envers' 
    compile (group: 'org.hibernate', name: 'hibernate-ehcache') {
        exclude(module: 'ehcache-core')
    }
    compile group: 'org.hibernate', name: 'hibernate-validator'
    compile group: 'org.jadira.usertype', name: 'usertype.core', version: usertype_core_version
    compile (group: 'org.liquibase', name: 'liquibase-core') {
        exclude(module: 'jetty-servlet')
    }
    compile group: 'com.mattbertolini', name: 'liquibase-slf4j', version: liquibase_slf4j_version
    
    compile group: 'org.springframework.boot', name: 'spring-boot-actuator' 
    compile group: 'org.springframework.boot', name: 'spring-boot-autoconfigure'
    compile group: 'org.springframework.boot', name: 'spring-boot-loader-tools'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-logging'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-aop'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-security'
    compile(group: 'org.springframework.boot', name: 'spring-boot-starter-web') {
        exclude module: 'spring-boot-starter-tomcat'
    }
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-thymeleaf'

    compile group: 'org.springframework.cloud', name: 'spring-cloud-cloudfoundry-connector'
    compile group: 'org.springframework.cloud', name: 'spring-cloud-spring-service-connector'
    compile group: 'org.springframework.cloud', name: 'spring-cloud-localconfig-connector'
    
    compile(group: 'org.springframework', name: 'spring-context-support') {
        exclude(module: 'quartz')
    }
    
    compile group: 'org.springframework.security', name: 'spring-security-config', version: spring_security_version
    compile group: 'org.springframework.security', name: 'spring-security-data', version: spring_security_version
    compile group: 'org.springframework.security', name: 'spring-security-web', version: spring_security_version
    
    compile(group: 'com.mangofactory', name: 'swagger-springmvc', version: swagger_springmvc_version) {
        exclude(module: 'jackson-module-scala_2.10')
        exclude(module: 'scalap')
        exclude(module: 'scala-compiler')
    }
    
    compile group: 'org.postgresql', name: 'postgresql', version: postgresql_version
    compile group: 'fr.ippon.spark.metrics', name: 'metrics-spark-reporter', version: metrics_spark_reporter_version
    
    testCompile group: 'com.jayway.awaitility', name: 'awaitility', version: awaility_version
    testCompile group: 'com.jayway.jsonpath', name: 'json-path' 
    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test'
    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-jetty'
    testCompile group: 'org.assertj', name: 'assertj-core-java8', version: assertj_core_version
    testCompile group: 'junit', name: 'junit'
    testCompile group: 'org.mockito', name: 'mockito-core'
    testCompile group: 'com.mattbertolini', name: 'liquibase-slf4j', version: liquibase_slf4j_version
    testCompile group: 'org.hamcrest', name: 'hamcrest-library'
    testCompile group: 'io.gatling.highcharts', name: 'gatling-charts-highcharts', version: gatling_version

    testCompile group: 'org.gebish', name: 'geb-junit4', version: geb_version
    testCompile group: 'org.seleniumhq.selenium', name: 'selenium-firefox-driver', version: selenium_version
    testRuntime group: 'org.seleniumhq.selenium', name: 'selenium-support', version: selenium_version
}

test {
    systemProperties "geb.build.reportsDir": "$reportsDir/geb"
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}

task stage(dependsOn: 'bootRepackage') {
}
